include(Backports/IncludeGuard)
include_guard(GLOBAL)

function(CMAKE_DEPENDENT_CACHE_VAR variable type doc default conditions force)
  list(APPEND type_list FILEPATH PATH STRING BOOL)
  list(FIND type_list ${type} type_found)
  if( type_found LESS 0 )
    message("CMAKE_DEPENDENT_CACHE_VAR error: variable type")
    message(FATAL_ERROR
      "'${type}' must be one of FILEPATH, PATH, STRING, or BOOL")
  endif()

  set(variable_AVAILABLE TRUE)
  foreach(condition ${conditions})
    string(REGEX REPLACE " +" ";"
      CMAKE_DEPENDENT_VARIABLE_DEP "${condition}")
    if(${CMAKE_DEPENDENT_VARIABLE_DEP})
    else()
      set(variable_AVAILABLE FALSE)
    endif()
  endforeach()

  if(variable_AVAILABLE)
    set(${variable} "${default}" CACHE "${type}" "${doc}")
    set(${variable} "${${variable}}" CACHE "${type}" "${doc}" FORCE)
  else()
    if(DEFINED ${variable})
      set(${variable} "${${variable}}" CACHE INTERNAL "${doc}")
    endif()
    set(${variable} "${force}" PARENT_SCOPE)
  endif()
endfunction()
